### Simplified JSON spec
# Only ascii printable character and space (0x20) can be used for string characters.
# Reference: https://www.json.org/json-ja.html

<json> ::=
 <element>

<value> ::=
  <object>
  | <array>
  | <string>
  | <number>
  | 'true'
  | 'false'
  | 'null'

<object> ::=
  '{' <ws> '}'
  | '{' <members> '}'

<members> ::=
  <member>
  | <member> ',' <members>

<member> ::=
  <ws> <string> <ws> ':' <element>

<array> ::=
  '[' <ws> ']'
  | '[' <elements> ']'

<elements> ::=
  <element>
  | <element> ',' <elements>

<element> ::=
  <ws> <value> <ws>

<string> ::=
  '"' <characters> '"'

<characters> ::=
  ''
  | <character> <characters>

<character> ::=
  'a' | 'b' | 'c' | 'd' | 'e' | 'f' | 'g' | 'h' | 'i' | 'j' | 'k' | 'l' | 'm' |
  'n' | 'o' | 'p' | 'q' | 'r' | 's' | 't' | 'u' | 'v' | 'w' | 'x' | 'y' | 'z' |
  'A' | 'B' | 'C' | 'D' | 'E' | 'F' | 'G' | 'H' | 'I' | 'J' | 'K' | 'L' | 'M' |
  'N' | 'O' | 'P' | 'Q' | 'R' | 'S' | 'T' | 'U' | 'V' | 'W' | 'X' | 'Y' | 'Z' |
  '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9' |
  ' ' | '!' | '#' | '$' | '%' | '&' | '"' | '(' | ')' | '*' | '+' | ',' | '-' |
  '.' | '/' | '[' | ']' | '^' | '_' | '`' | '{' | '|' | '}' | '~' | '\\' <escape>

<escape> ::=
  '"' | '\\' | '/' | 'b' | 'f' | 'n' | 'r' | 't'
  | 'u' <hex> <hex> <hex> <hex>

<hex> ::=
  <digit> | 'A' | 'B' | 'C' | 'D' | 'E' | 'F' | 'a' | 'b' | 'c' | 'd' | 'e' | 'f' 

<number> ::=
  <integer> <fraction> <exponent>

<integer> ::=
  <digit>
  | <onenine> <digits>
  | '-' <digit>
  | '-' <onenine> <digits>

<digits> ::=
  <digit>
  | <digit> <digits>

<digit> ::= '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9'
<onenine> ::= '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9'

<fraction> ::=
  '' | '.' <digits>

<exponent> ::=
  '' | 'E' <sign> <digits> | 'e' <sign> <digits>

<sign> ::=
  '' | '+' | '-'

<ws> ::=
  '' | ' ' <ws>
